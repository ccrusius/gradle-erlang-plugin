plugins {
  id "com.zoltu.git-versioning" version "2.0.28"
  id "com.gradle.plugin-publish" version "0.9.5"
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

targetCompatibility = 1.7

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  testCompile gradleTestKit()
  testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

task createTestProperties {
  def outFile = file("${buildDir}/resources/test/org.ccrusius.erlang.test.properties")
  outputs.file outFile
  doLast {
    outFile.parentFile.mkdirs()
    def unixBuildDir = buildDir.absolutePath.replaceAll("\\\\","/")
    def unixResultsDir = testResultsDir.absolutePath.replaceAll("\\\\","/")
    outFile.text = """\
      gradleBuildDir=${unixBuildDir}
      gradleTestResultsDir=${unixResultsDir}""".stripIndent()
  }
}

test.dependsOn(['createTestProperties'])

pluginBundle {
  website = "https://github.com/ccrusius/gradle-erlang-plugin"
  vcsUrl = "https://github.com/ccrusius/gradle-erlang-plugin"
  description = 'Erlang plugin for Gradle'

  plugins {
    erlangPlugin {
      id = 'org.ccrusius.erlang'
      displayName = 'Gradle Erlang plugin'
      tags = ['erlang']
    }
  }
}
